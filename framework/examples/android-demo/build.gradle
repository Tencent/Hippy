plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

Properties gradleProperties = new Properties()
def propFileDir = "$projectDir.absolutePath/../../android/gradle.properties"
def propFile = new File(propFileDir)
def verifyReleaseAar = false
def include_abi_armeabi_v7a = true
def include_abi_arm64_v8a = true
def include_abi_x86 = true
def include_abi_x86_64 = true
if (propFile.exists()) {
    gradleProperties.load(propFile.newDataInputStream())
    include_abi_armeabi_v7a = gradleProperties.getProperty('INCLUDE_ABI_ARMEABI_V7A')
    include_abi_arm64_v8a = gradleProperties.getProperty('INCLUDE_ABI_ARM64_V8A')
    include_abi_x86 = gradleProperties.getProperty('INCLUDE_ABI_X86')
    include_abi_x86_64 = gradleProperties.getProperty('INCLUDE_ABI_X86_64')
}


android {
    namespace 'com.openhippy.example'
    compileSdkVersion COMPILE_VERSION as int
    defaultConfig {
        applicationId "com.openhippy.example"
        minSdkVersion MIN_VERSION as int
        // noinspection ExpiredTargetSdkVersion
        //noinspection OldTargetApi
        targetSdkVersion TARGET_VERSION as int
        ndk  {
            if (include_abi_armeabi_v7a.toBoolean()) {
                abiFilters 'armeabi-v7a'
            }
            if (include_abi_arm64_v8a.toBoolean()) {
                abiFilters 'arm64-v8a'
            }
            if (include_abi_x86.toBoolean()) {
                abiFilters 'x86'
            }
            if (include_abi_x86_64.toBoolean()) {
                abiFilters 'x86_64'
            }

        }
    }
    signingConfigs {
        release {
            keyAlias 'key0'
            keyPassword 'hippydemokeypass!'
            storeFile file('./hippydemo.jks')
            storePassword 'hippydemostorepass!'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    if (verifyReleaseAar) {
        // options for maven packages or local libs integration
        // api 'com.tencent.hippy:hippy-common:2.11.2'
        // api 'com.tencent.hippy:hippy-debug:2.11.2'
        api fileTree(dir: 'libs', include: ['*.aar'])
    } else {
        api project(path: ':hippy-framework')
        compileOnly project(path: ':hippy-support')
        compileOnly project(path: ':renderer-native')
    }
    implementation deps.annotation
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

project.afterEvaluate {
    delete 'src/main/assets'
    copy{
        from "res"
        into "src/main/assets"
    }
}