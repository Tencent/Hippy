apply plugin: 'com.android.library'
android {
    compileSdkVersion 26
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

afterEvaluate {
    android.libraryVariants.each { variant ->
        dealAfterBuild(variant)
    }
}

def dealAfterBuild(variant) {
    if (variant.buildType.name == 'release') {

        File aarFile

        variant.outputs.each { output ->
            File file = output.outputFile
            if (file.name.endsWith(".aar")) {
                aarFile = file
            }
        }

        variant.assemble.finalizedBy task("dealAfterBuild${variant.name.capitalize()}") {
            doLast {
                unZipAar(aarFile)
            }
        }
    }
}
def unZipAar(file) {
    copy {
        from zipTree(file.absolutePath)
        into file.parentFile.absolutePath
        include 'classes.jar'
        rename 'classes.jar', 'support-ui.jar'
    }
}
