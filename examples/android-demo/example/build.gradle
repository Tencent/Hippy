apply plugin: 'com.android.application'

Properties gradleProperties = new Properties()
def propFileDir = "$projectDir.absolutePath/../../../android/sdk/gradle.properties"
def propFile = new File(propFileDir)
def verifyReleaseAar = false
def includeSupportui = true
if (propFile.exists()) {
    gradleProperties.load(propFile.newDataInputStream())
    includeSupportui = gradleProperties.getProperty('INCLUDE_SUPPORT_UI')
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.tencent.mtt.hippy.example"
        minSdkVersion 19
        // noinspection ExpiredTargetSdkVersion
        //noinspection OldTargetApi
        targetSdkVersion 29
        ndk  {
            abiFilters 'arm64-v8a'
        }
    }

    signingConfigs {
        release {
            keyAlias 'key0'
            keyPassword 'hippydemokeypass!'
            storeFile file('./hippydemo.jks')
            storePassword 'hippydemostorepass!'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    ext {
        MAVEN_USER = System.getenv('maven_username')
        MAVEN_PWD = System.getenv('maven_password')
        MAVEN_URL = System.getenv('maven_url')
    }
    repositories {
        if (MAVEN_URL != null) {
            maven {
                url MAVEN_URL
                credentials {
                    username MAVEN_USER
                    password MAVEN_PWD
                }
            }
        }
        google()
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    api project(path: ':android-sdk')
    //noinspection GradleDependency
    api 'com.github.bumptech.glide:glide:3.6.1'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    if (!includeSupportui.toBoolean()) {
        implementation 'com.tencent.mtt:support-ui:99.2.3'
    }
}
