apply plugin: 'com.android.application'

Properties gradleProperties = new Properties()
def propFileDir = "$projectDir.absolutePath/../../../android/sdk/gradle.properties"
def propFile = new File(propFileDir)
def verifyReleaseAar = false
def includeSupportui = true
if (propFile.exists()) {
    gradleProperties.load(propFile.newDataInputStream())
    includeSupportui = gradleProperties.getProperty('INCLUDE_SUPPORT_UI')
}

android {
    compileSdkVersion 29
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId "com.tencent.mtt.hippy.example"
        minSdkVersion 14
        // noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29
    }

    signingConfigs {
        release {
            keyAlias 'key0'
            keyPassword 'hippydemokeypass!'
            storeFile file('./hippydemo.jks')
            storePassword 'hippydemostorepass!'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }
}

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0+'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    ext {
        Properties localProperties = new Properties()
        InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
        localProperties.load(inputStream)

        MAVEN_USER = localProperties.getProperty('maven_username')
        MAVEN_PWD = localProperties.getProperty('maven_password')
        MAVEN_URL = localProperties.getProperty('maven_url')
    }
    repositories {
        if (MAVEN_URL != null) {
            maven {
                url MAVEN_URL
                credentials {
                    username MAVEN_USER
                    password MAVEN_PWD
                }
            }
        }
        google()
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    if (verifyReleaseAar) {
        api (name: 'android-sdk-release', ext: 'aar')
    } else {
        api project(path: ':android-sdk')
    }
    api 'com.github.bumptech.glide:glide:3.6.1'
    api 'com.android.support:support-v4:28.0.0'
    if (!includeSupportui) {
        implementation 'com.tencent.mtt:support-ui:99.1.0'
    }
}
