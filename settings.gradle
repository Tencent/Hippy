import java.nio.file.Paths

include 'connector-support'
project(':connector-support').projectDir = new File('framework/android/connector/support')

include 'connector-dom'
project(':connector-dom').projectDir = new File('framework/android/connector/dom')

include 'hippy-support'
project(':hippy-support').projectDir = new File('modules/android/hippy_support')

include 'hippy-framework'
project(':hippy-framework').projectDir = new File('framework/android')

include 'serialization'
project(':serialization').projectDir = new File('modules/android/serialization')

include 'vfs'
project(':vfs').projectDir = new File('modules/vfs/android')

include 'devtools-integration'
project(':devtools-integration').projectDir = new File('devtools/devtools-integration/android')

include 'example'
project(':example').projectDir = new File('framework/examples/android-demo')

def modules = MODULES.split(',')
for (String module : modules) {
    String path = module.trim()
    def subSettingGradle
    if (Paths.get(path).isAbsolute()) {
        ArrayList<String> paths = module.split('/')
        paths.remove(0)
        def subProjectName = paths.join('-')
        subSettingGradle = path + '/settings.gradle'
        include "$subProjectName"
        project(":$subProjectName").projectDir = new File(path)
    } else {
        def subProjectName = 'connector-' + module.replace("/", "-")
        def projectPath = 'framework/android/connector/' + path
        subSettingGradle = projectPath + '/settings.gradle'
        include "$subProjectName"
        project(":$subProjectName").projectDir = new File(projectPath)
    }
    File target = file(subSettingGradle)
    if (target.exists()) {
        apply from: "./$subSettingGradle"
    }
}

